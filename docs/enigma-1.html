<html><head><link rel="stylesheet" href="publish.css"><title>a software craft</title></head><body><header><a href="index.html">a software craft</a></header><article><h1>Enigma: Encryption like it's 1939</h1><table class="full"><colgroup><col class="first"><col></colgroup><tr><td colspan="2">I'm currently reading the biography Alan Turing: The Enigma, by Andrew Hodges. So when I started to learn Clojure, I decided a software simulation of the Enigma machine would make an interesting first project. The Enigma was actually a family of electro-mechanical encryption devices. You can find lots of details if you're interested - I won't describe them all here. I'll show some basic concepts of the Enigma, but won't try to accurately represent any specific historical Enigma model.<br/><br/>This is novice Clojure code.</td></tr><tr><td>The Enigma contains a series of rotors. A rotor has 26 different positions. We define a rotate function that takes a position and returns a new position, using with simple modulo 26 arithmetic.</td><td><pre class="code">(deftest rotate-no-wrap
  (is (= (range 1 26) (map rotate (range 0 25)))))

(deftest rotate-wrap (is (= 0 (rotate 25))))

(defn rotate [position] (-> position inc (mod 26)))
</pre></td></tr><tr><td>The operator sets the initial position of each rotor. For each character input, the first rotor advances one position. Each rotor has a "notch" position - when the rotor reaches this position, it advances the next rotor. We define an advance-positions function that takes a list of rotor positions and a list of notch positions and returns a new list of rotor positions.<br/><br/>Say we have 3 rotors set to positions 0, 1, and 2. The rotor notches are at 0, 1, and 1. When we advance the rotors, the first one moves but its notch position is not reached so no other rotors move. In this case, we can construct a new list of positions by rotating the first position and appending the rest of the original positions.</td><td><pre class="code">(deftest one-rotor-advances
  (is (= '(1 1 2) (advance-positions '(0 1 2) '(0 1 1)))))

(defn advance-positions [positions notches]
  (cons (-> positions first rotate) (rest positions)))
</pre></td></tr><tr><td>Now with positions 0, 1, and 2 and notches at 1, 2, and 1, all rotors will rotate one position. Note the notch on the last rotor is not used as there is no "next" rotor after the last. In this case, we need to enhance the advance-positions function to construct the new list of positions recursively.</td><td><pre class="code">(deftest all-rotors-advance
  (is (= '(1 2 3) (advance-positions '(0 1 2) '(1 2 1)))))

(defn advance-positions [positions notches]
  (let [new-position (-> positions first rotate)]
    (cons
      new-position
      (if (and
            (= new-position (first notches)) 
            (not-empty (rest positions)))
        (advance-positions (rest positions) (rest notches))
        (rest positions)))))
</pre></td></tr><tr><td>Each rotor in the Enigma machine performs a simple character substitution for each letter A-Z. The substitution is described by a string, like "EKMFLGDQVZNTOWYHXUSPAIBRCJ". This means that, with the rotor in position 0, the first letter, A is changed to E, B to K, C to M, and on up to Z to J.<br/><br/>What happens when the rotor advances to a new position? I found it useful to see the letters in the substitution string as relative offsets: A to E is +4, B to K is +9. C to M is +10. So when the rotor advances to position 1, A is changed with the second offset, +9, to J, B +10 to L, etc. Z wraps around to the first offset, +4, and changes to D.<br/><br/>With a couple of helper functions, we define a function make-offsets that converts a rotor substitution string into a list of offsets.</td><td><pre class="code">(deftest substitutes-to-offsets
  (is (= (concat (range 1 14) (range 13 26))
         (make-offsets "BDFHJLNPRTVXZACEGIKMOQSUWY"))))

(defn letter-to-int [letter] (-> letter int (- (int \A))))
(defn decrease [position change] (-> position (- change) (mod 26)))

(defn make-offsets [substitutes]
  (map-indexed
    (fn [i letter] (-> letter letter-to-int (decrease i)))
    substitutes))
</pre></td></tr><tr><td>Now we can define a function apply-offset that uses the offsets and a rotor position to encrypt an input. (We use integers 0-25 instead of characters A-Z, so we don't have to keep converting between integers and characters.)</td><td><pre class="code">(deftest encrypt-no-wrap
  (let [offsets (make-offsets "ABXCDEFGHIJKLMNOPQRSTUVWYZ")]
    (is (= 22 (apply-offset 1 1 offsets)))))

(deftest encrypt-wrap
  (let [offsets (make-offsets "AXBCDEFGHIJKLMNOPQRSTUVWYZ")]
    (is (= 24 (apply-offset 2 25 offsets)))))

(defn increase [position change] (-> position (+ change) (mod 26)))

(defn apply-offset [input position offsets]
  (-> offsets
      (nth (increase position input))
      (increase input)))
</pre></td></tr><tr><td>The Enigma machine uses the output of each rotor as input to the next rotor. The last rotor is a "reflector" that sends its output back to the previous rotor. This is passed backwards through each rotor, ending with the first. How do we perform a "backwards" substitution? We find the letter that is changed into our current value. Using our sample rotor substitution string "EKMFLGDQVZNTOWYHXUSPAIBRCJ" in position 0, U is changed to A, so the backwards substitution of A is U, B is W, C is Y, etc. Once again, to handle rotation of the rotor, it's useful to create a list of offsets.<br/><br/>We rename our previous function to make-forward-offsets and add a function make-backward-offsets.</td><td><pre class="code">(deftest substitutes-to-backward-offsets
  (is (= (mapcat (fn [i] (list i (+ i 12))) (range 13 0 -1))
         (make-backward-offsets "BDFHJLNPRTVXZACEGIKMOQSUWY"))))

(defn int-to-letter [input] (-> input (+ (int \A)) char))

(defn make-backward-offsets [substitutes]
  (map
    (fn [i] (decrease (index-of substitutes (int-to-letter i)) i))
    (range 0 26)))
</pre></td></tr><tr><td>Now we can re-use apply-offset to do backward encryption.</td><td><pre class="code">(deftest backward-encrypt-no-wrap
  (let [offsets (make-backward-offsets "ABYCDEFGHIJKLMNOPQRSTUVWXZ")]
    (is (= 1 (apply-offset 23 1 offsets))))
  )

(deftest backward-encrypt-wrap
  (let [offsets (make-backward-offsets "AWBCDEFGHIJKLMNOPQRSTUVXYZ")]
    (is (= 2 (apply-offset 23 25 offsets))))
  )
</pre></td></tr></table></article></body></html>